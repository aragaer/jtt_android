// -*- Mode: Gradle; tab-width: 4; indent-tabs-mode: nil; -*-
// vim: et ts=4 sts=4 sw=4 syntax=groovy
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    repositories {
        google()
    }
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.1'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.google.dagger:dagger:2.10'
    implementation 'org.jetbrains:annotations-java5:15.0'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.5'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.5'
    testImplementation 'org.hamcrest:hamcrest-integration:1.3'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'com.luckycatlabs:SunriseSunsetCalculator:1.1'

    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

android {
    sourceSets {
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    compileSdkVersion 'android-29'
    buildToolsVersion '29.0.1'

    defaultConfig {
        testApplicationId "com.aragaer.jtt.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        minSdkVersion 14
        targetSdkVersion 29
    }

    signingConfigs {
        release {
            storeFile file(System.getenv('HOME')+'/'+RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    testOptions {
        animationsDisabled = true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
            signingConfig signingConfigs.release
            shrinkResources true
        }
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }

    // Class R is used, but usage will not be covered, so ignore this class from report
    classDirectories = fileTree(
        dir: './build/intermediates/classes/debug',
        excludes: ['**/R.class',
                   '**/R$*.class',
                   '**/Dagger*Component.class', // covers component implementations
                   '**/Dagger*Component$Builder.class', // covers component builders
                   '**/*Module_*Factory.class'
    ])
    sourceDirectories = files('src/main/java')
    executionData = files('jacoco/testDebugUnitTest.exec')
}

task jacocoTestReportQuick(type: JacocoReport, dependsOn: "test") {
    reports.html.enabled = true

    classDirectories = tasks.jacocoTestReport.classDirectories
    sourceDirectories = tasks.jacocoTestReport.sourceDirectories
    executionData = files('build/jacoco/testReleaseUnitTest.exec')
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(createDebugAndroidTestCoverageReport)) {
        android.defaultConfig.testInstrumentationRunnerArgument "notAnnotation", "androidx.test.filters.LargeTest"
    }
}

tasks.withType(Test) {
    testLogging.exceptionFormat = 'full'
}
